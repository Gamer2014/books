100 REM LASTWORD
110 GOSUB 1100
120 PRINT"enter no. of players"
130 GOSUB 2100
140 GOSUB 2900
150 PRINT"enter names........."
160 FOR N=1 TO K
170 PRINT "player. ";N:GOSUB 2200
180 LET V$(N)=K$
190 NEXT N
200 GOSUB 1300
210 DIM L(4,26)
220 GOSUB 3000
230 LET T=0
240 GOSUB 1200
250 IF T>0 THEN PRINT "last word by:"
260 IF T>0 THEN PRINT V$(LW)
270 PRINT
280 LET R=R+1
290 IF R>K THEN LET R=1
300 PRINT"enter a word:"
310 PRINT V$(R)
320 PRINT
330 IF K$<>"" THEN LET L=0
340 IF T=0 OR K$="" THEN GOTO 400
350 FOR P=4 TO 1 STEP -1
360 GOSUB 2700
370 LET L(P,A-64)=L(P,A-64)+1
380 IF L(P,A-64)>7 THEN LET L=P
390 NEXT P
400 LET X=4+W/2
410 FOR Y=F TO F+6
420 GOSUB 1700
430 IF T+Y>F+6 THEN PRINT W$(T+Y-F-6)
440 NEXT Y
450 IF L>0 THEN PRINT"new letter ";STR$(L);"> ";
460 GOSUB 2200
470 IF K$="" THEN GOTO 520
480 LET T=T+1
490 LET W$(T)=K$
500 LET LW=R
510 LET P=0
520 LET P=P+1
530 IF P<K AND T<60 THEN GOTO 240
540 PRINT
550 PRINT V$(LW);" wins{space*14}"
560 STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
