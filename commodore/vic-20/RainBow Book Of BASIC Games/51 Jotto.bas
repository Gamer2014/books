100 REM JOTTO
110 GOSUB 1100
120 GOSUB 2900
130 PRINT"enter a secret word "
140 PRINT:GOSUB 2200
150 LET A$=K$
160 LET L=LEN(A$):GOSUB 1200
170 LET X=0:LET Y=6:GOSUB 1700
180 PRINT"try ";STR$(L);"-letter words{space*2}"
190 LET T=0
200 LET S=0
210 LET K$=A$
220 FOR P=1 TO L
230 GOSUB 2600
240 LET V$(P)=P$
250 NEXT P
260 LET X=0:LET Y=8:GOSUB 1700:PRINT"{space*20}":GOSUB 1700:GOSUB 2200
270 IF LEN(K$)<>L THEN GOTO 260
280 LET T=T+1
290 LET X=0:LET Y=10:GOSUB 1700
300 PRINT " ";K$
310 LET X=0:LET Y=12
320 GOSUB 1700
330 IF K$=A$ THEN GOTO 460
340 FOR P=1 TO L
350 GOSUB 2600
360 FOR N=1 TO L
370 IF P$<>V$(N) THEN GOTO 410
380 LET S=S+1
390 LET V$(N)="*"
400 LET N=L
410 NEXT N
420 NEXT P
430 PRINT"score ";STR$(S);" "
440 PRINT:PRINT:PRINT
450 GOTO 200
460 GOSUB 1200:PRINT"correct "
470 PRINT:PRINT"you took ";STR$(T);" turns{space*2}"
480 STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
