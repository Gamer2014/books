100 REM GOLF
110 GOSUB 1100
120 PRINT "{space*3}zigzag golf"
130 LET H=1:LET S=0
140 LET N=10:GOSUB 1300:LET A=R-1:GOSUB 1300:LET B=R-1:IF ABS(B-A)>5 OR A+B=0 THEN GOTO 140
150 LET T=2:GOSUB 1400:GOSUB 2800:LET X=7:LET Y=1:GOSUB 1700:PRINT "hole ";H:LET X=1:LET Y=8:GOSUB 1700:PRINT "*"
160 LET X=1+A+B:LET Y=8-A+B:GOSUB 1700:PRINT "o":LET S=S+1
170 LET X=1:LET Y=13:GOSUB 1700:PRINT "zig ";:GOSUB 2100:LET C=K
180 LET X=11:LET Y=13:GOSUB 1700:PRINT "zag ";:GOSUB 2100:LET D=K
190 LET X=1:LET Y=8:LET T=.2:IF C=0 THEN GOTO 210
200 FOR N=1 TO C:GOSUB 1700:PRINT " ":LET X=X+1:LET Y=Y-1:GOSUB 1700:PRINT "*";:GOSUB 1400:NEXT N
210 IF D=0 THEN GOTO 230
220 FOR N=1 TO D:GOSUB 1700:PRINT " ":LET X=X+1:LET Y=Y+1:GOSUB 1700:PRINT "*";:GOSUB 1400:NEXT N
230 LET X=3:LET Y=14:GOSUB 1700:IF A<>C OR B<>D THEN PRINT "missed !";:GOTO 150
240 PRINT "sunk it !"
250 LET T=2:GOSUB 1400
260 LET H=H+1:IF H<10 THEN GOTO 140
270 GOSUB 2800:PRINT:PRINT:PRINT "you took ";S;" strokes"
280 PRINT:PRINT:IF S<18 THEN PRINT "a real pro !!":GOTO 310
290 IF S<27 THEN PRINT"shows promise !":GOTO 310
300 PRINT "keep practising !"
310 PRINT:PRINT:STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
