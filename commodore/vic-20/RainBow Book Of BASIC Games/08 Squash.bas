100 REM SQUASH
110 GOSUB 1100:LET P=0
120 PRINT "{space*5}squash"
130 LET T=2:GOSUB 1400:GOSUB 2800
140 LET P=0:LET X=1:LET Y=1:GOSUB 1700:PRINT "******************"
150 FOR Y=2 TO 15:LET X=1:GOSUB 1700:PRINT "*";:LET X=18:GOSUB 1700:PRINT "*";:NEXT Y
160 LET A=4:LET B=14:LET X=A:LET Y=B:GOSUB 1700:PRINT "o";:LET C=1:LET D=-1:LET E=6
170 LET F=14:LET X=E:LET Y=F:GOSUB 1700:PRINT " xxx "
180 LET X=A:LET Y=B:GOSUB 1700:PRINT ".";:LET A=A+C:LET B=B+D:LET X=A:LET Y=B
190 GOSUB 1700
200 PRINT "o";
210 IF A<>17 THEN GOTO 230
220 LET C=-1:LET P=P+1:LET Z=40:GOSUB 2400
230 IF A<>2 THEN GOTO 250
240 LET C=1:LET P=P+1:LET Z=40:GOSUB 2400
250 IF B<>2 THEN GOTO 270
260 LET D=1:LET P=P+2:LET Z=60:GOSUB 2400
270 IF B<14 THEN GOTO 330
280 IF ABS(A-E-2)>1 THEN GOTO 300
290 LET D=-1:LET P=P+2:LET Z=60:GOSUB 2400:GOTO 330
300 LET  X=6:LET Y=4:GOSUB 1700:PRINT "missed !":LET Z=20:GOSUB 2400
310 LET X=4:LET Y=6:GOSUB 1700:PRINT" points ={space*2}";STR$(P)
320 LET T=2:GOSUB 1400:GOSUB 2800:STOP
330 GOSUB 1600:IF K$="z" AND E>1 THEN LET E=E-1
340 IF K$="x" AND E<14 THEN LET E=E+1
350 LET X=E:LET Y=14:GOSUB 1700:PRINT " xxx ":LET T=.2:GOSUB 1400:GOTO 180
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
