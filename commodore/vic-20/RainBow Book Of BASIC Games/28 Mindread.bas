100 REM MINDREAD
110 GOSUB 1100
120 PRINT"number mindreading"
130 PRINT:PRINT
140 PRINT"think of a number"
150 PRINT
160 PRINT"1 to 100 but don't"
170 PRINT
180 PRINT"tell me what it is"
190 LET T=6:GOSUB 1400
200 GOSUB 2800
210 PRINT:PRINT"divided by 3 the"
220 PRINT:PRINT"remainder is? ";
230 GOSUB 2100
240 LET A=K
250 PRINT:PRINT"divided by 5 the"
260 PRINT:PRINT"remainder is? ";
270 GOSUB 2100
280 LET B=K
290 PRINT:PRINT"divided by 7 the"
300 PRINT:PRINT"remainder is? ";
310 GOSUB 2100
320 LET C=K
330 LET T=1:GOSUB 1400
340 GOSUB 2800
350 PRINT
360 PRINT:PRINT"shh! i'm thinking"
370 PRINT
380 LET T=4:GOSUB 1400
390 LET D=0
400 FOR N=1 TO 100
410 IF (N-A)/3<>INT((N-A)/3) THEN GOTO 450
420 IF (N-B)/5<>INT((N-B)/5) THEN GOTO 450
430 IF (N-C)/7<>INT((N-C)/7) THEN GOTO 450
440 LET D=N:LET N=100
450 NEXT N
460 IF D=0 THEN PRINT "i give up!":GOTO 510
470 PRINT "was it ";STR$(D);"? y or n";
480 GOSUB 1500
490 PRINT:PRINT
500 IF G$="y" THEN PRINT "ha! i read your mind":GOTO 540
510 PRINT:PRINT"ok! let's try again"
520 LET T=8:GOSUB 1400
530 GOTO 100
540 STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
