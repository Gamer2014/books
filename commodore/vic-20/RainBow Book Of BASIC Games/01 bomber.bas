100 REM BOMBER
110 GOSUB 1100:PRINT "{space*6}bomber"
120 LET T=2:GOSUB 1400:GOSUB 2800
130 DIM H(16)
140 LET N=6:FOR I=1 TO 14:GOSUB 1300:LET H(I+1)=R+2:NEXT I:LET H(1)=0:LET H(16)=0
150 FOR Y=15 TO 6 STEP -1:LET X=3:GOSUB 1700
160 FOR I=1 TO 16:IF H(I)>=16-Y THEN PRINT "+";:GOTO 180
170 PRINT " ";
180 NEXT I:PRINT:NEXT Y
190 LET A=0:LET B=1:LET D=1:LET F=0
200 LET X=A:LET Y=B:GOSUB 1700:PRINT "{space*5}":LET A=A+D
210 IF A<1 OR A>16 THEN GOTO 230
220 LET X=A:LET Y=B:GOSUB 1700:PRINT" <=> ";:GOTO 260
230 LET F=0:IF D=1 THEN LET D=-1:GOTO 200
240 LET X=A:LET Y=B:GOSUB 1700:PRINT "{space*5}";:LET B=B+1:LET D=1:IF B<14 THEN GOTO 200
250 LET T=4:GOSUB 1400:GOTO 360
260 LET T=.2:GOSUB 1400:IF H(A)+B=16 THEN GOTO 320
270 IF F=1 THEN GOTO 200
280 GOSUB 1600:IF K$<>" " THEN GOTO 200
290 LET F=1:LET X=A+2:FOR Y=B+1 TO 15:GOSUB 1700:PRINT "*";:LET T=0.1:GOSUB 1400
300 LET Z=100:GOSUB 2400:GOSUB 1700:PRINT " ";:LET T=0.1:GOSUB 1400:NEXT Y:LET H(A)=0
310 GOTO 200
320 LET T=.1:FOR I=1 TO 8:GOSUB 1700:PRINT "{space*5}":LET Z=20:GOSUB 2400
330 GOSUB 1400:GOSUB 1700:PRINT
340 GOSUB 1700:PRINT " xxx ":LET T=4:GOSUB 1400
350 GOSUB 2800:PRINT:PRINT:PRINT "you didn't make it!":GOTO 380
360 GOSUB 2800:PRINT:PRINT:PRINT "a safe landing !":PRINT
370 PRINT "have a nice day !"
380 PRINT:PRINT:PRINT:STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
