100 REM DUNGEON
110 DIM M(100)
120 DIM Q(100)
130 GOSUB 1100
140 FOR I=0 TO 4
150 FOR J=1 TO 9
160 LET Q(18*I+J)=90+J-20*I
170 LET Q(18*I+J+9)=90-J-20*I
180 LET M(18*I+J)=2*I+10*J+1
190 LET M(18*I+J+9)=2*I-10*J+102
200 NEXT J:NEXT I
210 LET X=0:LET Y=2:GOSUB 1700
220 PRINT "{space*2}***********"
230 FOR N=1 TO 9
240 PRINT "{space*2}*+++++++++*"
250 NEXT N
260 PRINT "{space*2}***********"
270 LET PT=0:LET MT=0
280 LET X=0:LET Y=14:GOSUB 1700
290 PRINT "enter princess' move"
300 GOSUB 1500:LET K$=G$:LET P=1:GOSUB 2700:LET Q=A-48
310 IF Q<1 OR Q>9 THEN GOTO 300
320 GOSUB 1700:PRINT"enter monster's move"
330 GOSUB 1500:LET K$=G$:LET P=1:GOSUB 2700:LET M=A-48
340 IF M<1 OR M>9 THEN GOTO 330
350 GOSUB 1700:PRINT "now the moves begin "
360 LET T=1:GOSUB 1400
370 GOSUB 1700:PRINT "{space*20}"
380 LET K=0:LET B=0
390 LET QY=9:LET QX=1
400 LET MY=1:LET MX=1
410 FOR G=1 TO Q
420 LET PT=PT+1:LET T=0.1:GOSUB 1400
430 IF PT>81 THEN GOTO 630
440 LET C=INT(Q(PT)/10):LET L=Q(PT)-10*C
450 LET X=QX+2:LET Y=QY+2:GOSUB 1700:PRINT "+";
460 LET X=C+2:LET Y=L+2:GOSUB 1700
470 PRINT "p";
480 IF MY=L AND MX=C THEN GOTO 700
490 LET QY=L
500 LET QX=C
510 NEXT G
520 FOR G=1 TO M
530 LET MT=MT+1:LET T=0.1:GOSUB 1400
540 IF MT>81 THEN GOTO 630
550 LET B=INT(M(MT)/10):LET K=M(MT)-10*B
560 LET X=MX+2:LET Y=MY+2:GOSUB 1700:PRINT "+";
570 LET X=B+2:LET Y=K+2:GOSUB 1700
580 PRINT "m";
590 IF K=QY AND B=QX THEN GOTO 700
600 LET MY=K:LET MX=B
610 NEXT G
620 GOTO 410
630 LET T=3:GOSUB 1400:GOSUB 1200
640 LET X=0:LET Y=6:GOSUB 1700
650 PRINT"congratulations! the"
660 PRINT:PRINT"choice of ";Q;" for"
670 PRINT:PRINT"princess and ";M;" for"
680 PRINT:PRINT"monster has worked"
690 PRINT:STOP
700 LET T=3:GOSUB 1400:GOSUB 1200
710 LET X=0:LET Y=6:GOSUB 1700
720 PRINT"too bad - you met"
730 PRINT:PRINT"in cell ";L;C;" and"
740 PRINT:PRINT"the princess was"
750 PRINT:PRINT"caught !"
760 PRINT:STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
