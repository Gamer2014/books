100 REM MASTER
110 GOSUB 1100
120 PRINT "{space*5}mastermind"
130 LET T=4:GOSUB 1400:GOSUB 2800
140 DIM A(3)
150 DIM B(3)
160 LET Y1=0:LET T1=255
170 LET M=0:FOR I=1 TO 3
180 LET N=9:GOSUB 1300
190 LET A(I)=R
200 IF I-1=0 THEN GOTO 240
210 LET F=0:FOR J=1 TO I-1
220 IF A(I)=A(J) THEN LET F=1
230 NEXT J:IF F=1 THEN GOTO 190
240 LET M=10*M+A(I):NEXT I
250 FOR I=1 TO 20
260 GOSUB 2800
270 PRINT:PRINT "guess number ";STR$(I)
280 PRINT:PRINT "enter guess ";
290 GOSUB 2100
300 IF K<111 OR K>999 THEN GOTO 260
310 LET B(1)=INT(K/100)
320 LET B(2)=INT((K-100*INT(K/100))/10)
330 LET B(3)=K-10*INT(K/10)
340 LET C=0:LET D=0
350 FOR J=1 TO 2
360 IF A(J)<>B(J+1) THEN GOTO 380
370 LET C=C+1
380 IF A(J+1)<>B(J) THEN GOTO 400
390 LET C=C+1
400 NEXT J
410 IF A(1)<>B(3) THEN GOTO 430
420 LET C=C+1
430 IF A(3)<>B(1) THEN GOTO 450
440 LET C=C+1
450 FOR J=1 TO 3
460 IF A(J)<>B(J) THEN GOTO 480
470 LET D=D+1
480 NEXT J
490 IF D=3 THEN GOTO 610
500 IF C=0 THEN GOTO 520
510 PRINT:PRINT STR$(C);" wrong position"
520 IF D=0 THEN GOTO 540
530 PRINT:PRINT STR$(D);" correct"
540 IF C+D<>0 THEN GOTO 560
550 PRINT:PRINT "nothing correct"
560 LET T=2:GOSUB 1400
570 NEXT I
580 PRINT:PRINT"that's 20 guesses"
590 PRINT:PRINT"number was ";STR$(M)
600 GOTO 620
610 PRINT:PRINT "you got it !"
620 STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
