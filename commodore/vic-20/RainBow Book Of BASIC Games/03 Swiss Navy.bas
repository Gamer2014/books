100 REM NAVY
110 GOSUB 1100
120 DIM B(6,8)
130 DIM S$(6)
140 PRINT"{space*4}swiss navy"
150 FOR I=1 TO 6:FOR J=1 TO 8:READ B(I,J):NEXT J:NEXT I
160 DATA 0,1,1,1,1,0,0,0
170 DATA 0,0,0,0,0,0,2,0
180 DATA 3,3,0,0,0,0,2,0
190 DATA 0,0,0,4,0,0,2,0
200 DATA 5,0,0,0,0,0,0,0
210 DATA 0,0,5,0,0,0,0,4
220 FOR I=1 TO 6:READ S$(I):NEXT I
230 DATA "*","b","c","d","p","m"
240 LET T=2:GOSUB 1400:GOSUB 2800:PRINT
250 PRINT "{space*3}a b c d e f g h":PRINT
260 PRINT " 1 + + + + + + + +":PRINT
270 PRINT " 2 + + + + + + + +":PRINT
280 PRINT " 3 + + + + + + + +":PRINT
290 PRINT " 4 + + + + + + + +":PRINT
300 PRINT " 5 + + + + + + + +":PRINT
310 PRINT " 6 + + + + + + + +":PRINT
320 LET B=0:LET H=0
330 LET B=B+1
340 LET X=1:LET Y=14:GOSUB 1700:PRINT "{space*19}"
350 GOSUB 1700:PRINT "row ";
360 GOSUB 1500:GOSUB 3100:LET R=GG-48:IF R<1 OR R>6 THEN GOTO 360
370 PRINT G$;" col ";
380 GOSUB 1500:GOSUB 3100:LET C=GG-64:IF C<1 OR C>8 THEN GOTO 380
390 PRINT G$;"{space*3}";
400 LET P=B(R,C):IF P=0 THEN PRINT "miss";:GOTO 430
410 IF P>4 THEN GOTO 430
420 PRINT "hit!":LET B(R,C)=0:LET H=H+1
430 LET X=2*C+1:LET Y=2*R+1:GOSUB 1700:PRINT S$(P+1);
440 IF P<>5 THEN GOTO 470
450 LET X=1:LET Y=15:GOSUB 1700:PRINT "hit mountain !";:LET T=2:GOSUB 1400
460 GOTO 520
470 LET T=3:GOSUB 1400:IF H<11 THEN GOTO 330
480 GOSUB 2800:PRINT:PRINT:PRINT "you took ";STR$(B);" bombs"
490 PRINT:PRINT "to sink the navy"
500 PRINT:IF B<30 THEN PRINT "{space*2}good flying !":GOTO 520
510 PRINT "you need practice !"
520 PRINT:PRINT:PRINT:STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
