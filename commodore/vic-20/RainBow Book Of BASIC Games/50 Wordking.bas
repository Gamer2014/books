100 REM WORDKING
110 GOSUB 1100
120 LET N=26
130 GOSUB 1300
140 GOSUB 2500:LET S=0
150 GOSUB 1200:LET X=0:LET Y=6:GOSUB 1700
160 PRINT"what gift have you ":PRINT
170 PRINT" brought for the{space*3}":PRINT
180 PRINT"king of wordomania?":PRINT
190 GOSUB 2200
200 FOR P=1 TO LEN(K$)
210 GOSUB 2600
220 IF P$=R$ THEN LET P=99
230 NEXT P
240 PRINT"{space*19}"
250 IF P=100 THEN PRINT " * gift accepted *"
260 IF P<100 THEN PRINT " --gift refused-- "
270 PRINT
280 LET S=S+1
290 IF P<100 THEN LET S=S+1
300 PRINT
310 PRINT"have you guessed the":PRINT
320 PRINT"king's letter y/n?":PRINT
330 GOSUB 1500
340 GOSUB 1200:LET X=0:LET Y=4:GOSUB 1700
350 IF G$="n" THEN GOTO 440
360 IF G$<>"y" THEN GOTO 310
370 PRINT"{space*2}enter the king's{space*2}":PRINT
380 PRINT"{space*2}favourite letter{space*2}":PRINT
390 GOSUB 2200
400 IF K$=R$ THEN GOTO 510
410 LET S=S+10
420 GOSUB 1200
430 PRINT"bad luck - not ";K$;"{space*2}"
440 PRINT"your score is ";STR$(S);"{space*3}"
450 PRINT"{space*20}"
460 LET T=2:GOSUB 1400:IF S<30 THEN GOTO 140
470 PRINT"the king is angry{space*2}":PRINT
480 PRINT"run for your life{space*2}":PRINT
490 PRINT"the letter was ";R$;:PRINT
500 GOTO 540
510 PRINT" <<quite correct>> ":PRINT
520 PRINT"you win the freedom":PRINT
530 PRINT"{space*3}of wordomania{space*3}":PRINT
540 STOP
1000 REM VIC20 SUBROUTINES
1100 W=20:D=23
1110 F=8:GOSUB 1200:RETURN
1200 PRINT CHR$(147)
1210 FOR LL=1 TO 8:PRINT:NEXT LL
1220 PRINT;
1230 RETURN
1300 R=INT(RND(1)*N+1):RETURN
1400 FOR Z=1 TO 1000*T:NEXT Z:RETURN
1500 GET G$:IF G$="" THEN 1500
1510 RETURN
1600 GET K$:RETURN
1700 PRINT CHR$(19)
1710 IF Y>0 THEN FOR LL=1 TO Y:PRINT CHR$(17);:NEXT LL
1720 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1730 RETURN
1800 IF X>0 THEN FOR LL=1 TO X:PRINT CHR$(29);:NEXT LL
1810 RETURN
1900 PRINT CHR$(145);:RETURN
2000 PRINT CHR$(29);:RETURN
2100 INPUT K:RETURN
2200 INPUT K$:RETURN
2300 PRINT CHR$(147):RETURN
2400 POKE 36878,15:POKE 36876,Z+128
2410 FOR M=1 TO 50:NEXT M
2420 POKE 36876,0:RETURN
2500 R$=CHR$(R+64):RETURN
2600 P$=MID$(K$,P,1):RETURN
2700 GOSUB 2600:A=ASC(P$):RETURN
2800 PRINT CHR$(147):RETURN
2900 DIM V$(26):RETURN
3000 DIM W$(60):RETURN
3100 GG=ASC(G$):RETURN
